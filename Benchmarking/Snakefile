import os
# read config info into this namespace
configfile: "config.yaml"

include: "bamQC"

rule all:
    input:
        expand("{mapping_folder}/{sample}.sort.bam.bai", mapping_folder=config['mappings'], sample=config["samples"]),
        expand("{mapping_folder}/{sample}.sort.bam", mapping_folder=config['mappings'], sample=config["samples"]),
        os.path.join(config['mappings'], "BamQC.pdf"),
        "qc/multiqc.html"

rule fastqc:
    input:
        lambda wildcards: config["samples"][wildcards.sample]['reads'][int(wildcards.read)-1]
    output:
        html="FastQC/{sample}_R{read}_fastqc.html",
        zip="FastQC/{sample}_R{read}_fastqc.zip"
    params: ""
    wrapper:
        "0.17.4/bio/fastqc"

rule multiqc:
    input:
        expand("FastQC/{sample}_R{read}_fastqc.html", sample=config["samples"], read=[1,2]),
    output:
        "qc/multiqc.html"
    params:
        ""  # Optional: extra parameters for multiqc.
    log:
        "Logs/MultiQC/multiqc.log"
    wrapper:
        "0.19.3/bio/multiqc"

rule hisat:
    input:
        reads = lambda wildcards: config["samples"][wildcards.sample]['reads']
    output:
        temp("{mapping_folder}/{sample}.bam")
    params:
        idx = config["index"],
        extra = '--known-splicesite-infile ' + config["splice_sites"]
    benchmark:
        "Benchmarks/{sample}.hisat.benchmark.txt"
    log:
        "Logs/{mapping_folder}/{sample}_hisat_map.txt"
    threads: 8                          # optional, defaults to 1
    wrapper:
      "0.17.4/bio/hisat2"

rule sort_bam:
    input:
        rules.hisat.output
    output:
        "{mapping_folder}/{sample}.sort.bam"
    params:
        "-m 4G"
    threads: 8
    wrapper:
        "0.17.4/bio/samtools/sort"
        
rule samtools_index:
    input:
        rules.sort_bam.output
    output:
        "{mapping_folder}/{sample}.sort.bam.bai"
    wrapper:
        "0.17.4/bio/samtools/index"
